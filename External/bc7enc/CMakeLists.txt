srkSetSubProject("")
set(OUTPUT_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}bc7enc)
set(SLIB_PREFIX ${PROJECT_BINARY_DIR}/install/lib/${OUTPUT_NAME})
set(SLIB_R ${SLIB_PREFIX}${CMAKE_RELEASE_POSTFIX}${SRK_SLIB_SUFFIX})
set(SLIB_D ${SLIB_PREFIX}${CMAKE_DEBUG_POSTFIX}${SRK_SLIB_SUFFIX})

set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_BC7ENC_NAME ${PROJECT_NAME})
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_BC7ENC_INCLUDE ${PROJECT_BINARY_DIR}/install/include)
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_BC7ENC_SLIB ${SLIB_PREFIX}${SRK_BUILD_TYPE_SUFFIX}${SRK_SLIB_SUFFIX})
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_BC7ENC_SLIB_R ${SLIB_R})
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_BC7ENC_SLIB_D ${SLIB_D})

include(ExternalProject)

srkCreateExternalProjectCmakeArgs(cmakeArgs)

ExternalProject_Add(
    ${PROJECT_NAME}
    PREFIX ${PROJECT_BINARY_DIR}
    GIT_REPOSITORY https://github.com/richgel999/bc7enc.git
    GIT_TAG f66c2e4
    GIT_SHALLOW 1
    GIT_PROGRESS 1
    TMP_DIR tmp
    STAMP_DIR stamp
    DOWNLOAD_DIR download
    INSTALL_DIR install
    SOURCE_DIR src
    BINARY_DIR ${SRK_PROJECT_BUILD_ROOT_DIR}
    UPDATE_COMMAND
        ${CMAKE_COMMAND}
        -DTARGET_DIR=${PROJECT_BINARY_DIR}/src
        -DTARGET_HASH=f66c2e489b07138f2673a2fb3d27c1aa1d565c48
        -DTARGET_REPOSITORY=https://github.com/richgel999/bc7enc.git
        -DTARGET_BRANCH=master
        -P ${SRK_SOURCE_DIR}/CMake/GitUpdate.cmake
    PATCH_COMMAND
        ${CMAKE_COMMAND}
        -DSRC_DIR=${PROJECT_BINARY_DIR}/src
        -P ${CMAKE_CURRENT_SOURCE_DIR}/patch.cmake
    BUILD_BYPRODUCTS
        ${SLIB_R}
        ${SLIB_D}
    CMAKE_ARGS
        -DCMAKE_SHARED_LIBRARY_PREFIX=${CMAKE_SHARED_LIBRARY_PREFIX}
        -DCMAKE_STATIC_LIBRARY_PREFIX=${CMAKE_STATIC_LIBRARY_PREFIX}
        ${cmakeArgs}
)

set_property(TARGET ${PROJECT_NAME} PROPERTY FOLDER ${SRK_EXTERNAL_DIR_NAME})