srkSetProject()
if (SRK_OS_WINDOWS)
    set(OUTPUT_NAME zlib)
else ()
    set(OUTPUT_NAME libz)
endif ()
set(LIB_PREFIX ${PROJECT_BINARY_DIR}/install/lib/${OUTPUT_NAME})
set(LIB_R ${LIB_PREFIX}${SRK_LIB_SUFFIX})
set(LIB_D ${LIB_PREFIX}d${SRK_LIB_SUFFIX})

set_property(GLOBAL PROPERTY SRKPROJ_ZLIB_NAME ${PROJECT_NAME})
set_property(GLOBAL PROPERTY SRKPROJ_ZLIB_INCLUDE ${PROJECT_BINARY_DIR}/install/include)
set_property(GLOBAL PROPERTY SRKPROJ_ZLIB_LIB ${LIB_PREFIX}${SRK_BUILD_TYPE_SUFFIX}${SRK_LIB_SUFFIX})
set_property(GLOBAL PROPERTY SRKPROJ_ZLIB_LIB_R ${LIB_R})
set_property(GLOBAL PROPERTY SRKPROJ_ZLIB_LIB_D ${LIB_D})
set_property(GLOBAL PROPERTY SRKPROJ_ZLIB_DLL ${PROJECT_BINARY_DIR}/install/${SRK_INSTALL_DLL_DIR_NAME}/${OUTPUT_NAME}${SRK_BUILD_TYPE_SUFFIX}${SRK_DLL_SUFFIX})

include(ExternalProject)

srkCreateExternalProjectCmakeArgs(cmakeArgs)

ExternalProject_Add(
    ${PROJECT_NAME}
    PREFIX ${PROJECT_BINARY_DIR}
    GIT_REPOSITORY https://github.com/madler/zlib.git
    GIT_TAG v1.2.12
    GIT_SHALLOW 1
    GIT_PROGRESS 1
    TMP_DIR tmp
    STAMP_DIR stamp
    DOWNLOAD_DIR download
    INSTALL_DIR install
    SOURCE_DIR src
    BINARY_DIR ${SRK_BUILD_DIR}
    UPDATE_COMMAND ""
    PATCH_COMMAND
        ${CMAKE_COMMAND}
        -DGIT_ROOT=${PROJECT_BINARY_DIR}/src
        -DCMAKE_LISTS_FILE=${CMAKE_CURRENT_BINARY_DIR}/src/CMakeLists.txt
        -DSRK_DLL_SUFFIX=${SRK_DLL_SUFFIX}
        -P ${CMAKE_CURRENT_SOURCE_DIR}/patch.cmake
    BUILD_BYPRODUCTS 
        ${LIB_R}
        ${LIB_D}
    CMAKE_ARGS
        -DBUILD_SHARED_LIBS=1
        ${cmakeArgs}
)

set_property(TARGET ${PROJECT_NAME} PROPERTY FOLDER ${SRK_EXTERNAL_DIR_NAME})