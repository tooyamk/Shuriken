srkSetSubProject("")
set(OUTPUT_NAME libpng)
set(LIB_PREFIX ${PROJECT_BINARY_DIR}/install/lib/${OUTPUT_NAME})
set(LIB_R ${LIB_PREFIX}${CMAKE_RELEASE_POSTFIX}${SRK_LIB_SUFFIX})
set(LIB_D ${LIB_PREFIX}${CMAKE_DEBUG_POSTFIX}${SRK_LIB_SUFFIX})
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_LIBPNG_NAME ${PROJECT_NAME})
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_LIBPNG_INCLUDE ${PROJECT_BINARY_DIR}/install/include)
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_LIBPNG_LIB ${LIB_PREFIX}${SRK_BUILD_TYPE_SUFFIX}${SRK_LIB_SUFFIX})
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_LIBPNG_LIB_R ${LIB_R})
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_LIBPNG_LIB_D ${LIB_D})
set_property(GLOBAL PROPERTY SRKPROJ_EXTERNAL_LIBPNG_DLL ${PROJECT_BINARY_DIR}/install/${SRK_INSTALL_DLL_DIR_NAME}/${OUTPUT_NAME}${SRK_BUILD_TYPE_SUFFIX}${SRK_DLL_SUFFIX})

get_property(SRKPROJ_EXTERNAL_ZLIB_NAME GLOBAL PROPERTY "SRKPROJ_EXTERNAL_ZLIB_NAME")
get_property(SRKPROJ_EXTERNAL_ZLIB_INCLUDE GLOBAL PROPERTY "SRKPROJ_EXTERNAL_ZLIB_INCLUDE")
get_property(SRKPROJ_EXTERNAL_ZLIB_LIB GLOBAL PROPERTY "SRKPROJ_EXTERNAL_ZLIB_LIB")

set(gitTag v1.6.39)
srkCheckExternalProjectVersion_GitTag(${PROJECT_BINARY_DIR} ${PROJECT_BINARY_DIR}/src ${gitTag})

include(ExternalProject)

srkCreateExternalProjectCmakeArgs(cmakeArgs)

ExternalProject_Add(
    ${PROJECT_NAME}
    PREFIX ${PROJECT_BINARY_DIR}
    GIT_REPOSITORY https://github.com/glennrp/libpng.git
    GIT_TAG ${gitTag}
    GIT_SHALLOW 1
    GIT_PROGRESS 1
    GIT_CONFIG core.autocrlf=false
    TMP_DIR tmp
    STAMP_DIR stamp
    DOWNLOAD_DIR download
    INSTALL_DIR install
    SOURCE_DIR src
    BINARY_DIR ${SRK_PROJECT_BUILD_ROOT_DIR}
    UPDATE_COMMAND ""
    PATCH_COMMAND
        ${CMAKE_COMMAND}
        -DSRC_DIR=${PROJECT_BINARY_DIR}/src
        -P ${CMAKE_CURRENT_SOURCE_DIR}/patch.cmake
    BUILD_BYPRODUCTS 
        ${LIB_R}
        ${LIB_D}
    CMAKE_ARGS
        -DPNG_BUILD_ZLIB=ON
        -DPNG_STATIC=OFF
        -DPNG_TESTS=OFF
        -DPNG_EXECUTABLES=OFF
        -DZLIB_INCLUDE_DIRS=${SRKPROJ_EXTERNAL_ZLIB_INCLUDE}
        -DZLIB_LIBRARIES=${SRKPROJ_EXTERNAL_ZLIB_LIB}
        ${cmakeArgs}
)

add_dependencies(${PROJECT_NAME} ${SRKPROJ_EXTERNAL_ZLIB_NAME})
set_property(TARGET ${PROJECT_NAME} PROPERTY FOLDER ${SRK_EXTERNAL_DIR_NAME})